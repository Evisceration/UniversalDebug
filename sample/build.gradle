apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    signingConfigs {
        release {
            def store = project.findProperty("sign_store") ?: "debug.keystore"
            storeFile file(store)

            if ("debug.keystore".equals(store)) {
                logger.quiet('Not signing the build!')
            } else {
                logger.quiet('Signing the build!')
                storePassword project.property("sign_store_pass")
                keyAlias project.property("sign_key_alias")
                keyPassword project.property("sign_key_pass")
            }
        }
    }

    defaultConfig {
        applicationId "at.amartinz.universaldebug.sample"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName '1.0.0'

        def String gitRevision = getGitCommitHash()
        if (gitRevision != null) {
            versionName(versionName + '-git-' + gitRevision)
        }

        signingConfig signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.4.0'

    compile project(':universal-debug')
    compile project(':universal-debug-fabric')
    compile project(':universal-debug-firebase')
}

// firebase
apply plugin: 'com.google.gms.google-services'
