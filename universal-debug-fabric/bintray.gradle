apply plugin: 'com.github.dcendents.android-maven'

def libraryGroupId = 'at.amartinz'
def libraryArtifactId = 'universal-debug-fabric'
def libraryVersion = '1.0.0'

group libraryGroupId

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId libraryGroupId
            artifactId libraryArtifactId
            version libraryVersion

            name 'universal-debug-fabric'
            description 'An extension for UniversalDebug which includes Fabric tools'
            url 'https://github.com/Evisceration/UniversalDebug'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id 'evisceration'
                    name 'Alexander Martinz'
                    email 'eviscerationls@gmail.com'
                }
            }
            scm {
                connection 'https://github.com/Evisceration/UniversalDebug.git'
                developerConnection 'git@github.com:Evisceration/UniversalDebug.git'
                url 'https://github.com/Evisceration/UniversalDebug'
            }
        }
    }
}

//------------------------

apply plugin: 'com.jfrog.bintray'

version libraryVersion

if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
        failOnError false
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintray_user
    key = bintray_api_key

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'universal-debug-fabric'
        desc = 'An extension for UniversalDebug which includes Fabric tools'
        websiteUrl = 'https://github.com/Evisceration/UniversalDebug'
        vcsUrl = 'https://github.com/Evisceration/UniversalDebug.git'
        licenses = ["MIT"]
        publicDownloadNumbers = false
        version {
            gpg {
                sign = true
            }
        }
    }
}

bintrayUpload {
    dependsOn javadocJar
    dependsOn sourcesJar
}
